BACKUP "weidu_external/backup/iwdspells"
AUTHOR ~DavidW~
VERSION ~v1~

AUTO_EVAL_STRINGS
ASK_EVERY_COMPONENT

ALWAYS
   OUTER_SPRINT scsroot dw_iwdspells
   OUTER_SPRINT resource_loc dw_iwdspells_resource
   OUTER_SPRINT iwdspells_data data
   OUTER_SPRINT iwdspells_lib lib
   OUTER_SPRINT iwdspells_resource resource
   OUTER_SPRINT iwdspells_trabase tra
   OUTER_SPRINT sfo_loc sfo
   OUTER_SPRINT ssl_loc ssl
   OUTER_SPRINT inifile "%scsroot%.ini"
   INCLUDE ~%scsroot%/%iwdspells_lib%/always.tph~
   OUTER_SET do_not_biff=1   // set to 0 and most resources get biffed
   OUTER_SET get_all_languages=0  // set to 0 when testing the resource-collection install, because it speeds things up a lot
END

////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////
////	Language commands
////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////

LANGUAGE
"English"
ENGLISH
 ~dw_iwdspells/tra/english/setup.tra~


////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////
////	test space
////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////

/*
BEGIN "Test" DESIGNATED 0

LAF run STR_VAR file=test location=test END

*/

////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////
////	BG(2)EE bit - runs to install resources
////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////

BEGIN @5 DESIGNATED 1000 // install arcane resources
REQUIRE_PREDICATE GAME_IS "bgee bg2ee eet" @6

LAF make_label STR_VAR label=dw#iwd_arcane END

LAF include STR_VAR file=lib_spell_style.tph location="%iwdspells_lib%" END
LAF include STR_VAR file=install_iwd_spell_resources.tpa location="%iwdspells_lib%" END

LAF install_iwd_spell_resources
           INT_VAR
				truncate_at_level=20
		   STR_VAR
               scroll_shadow_list=iwd_arcane.2da
               spell_list=arcane_resrefs.txt
               lookup=iwd_strref.txt
               smtables_list=arcane_smtable.txt
               scroll_price_list=scroll_prices_arcane.txt
               proj_list=arcane_proj.txt
               tra=iwdspells.tra
               biff="dw#iwdm"
               afterwards="arcane_spells_postproduction"
               afterwards_tra="dw_iwdspells_arcane.tra"
               bam_copy="bam_copy_arcane.2da"
               green_bams="green_spells_arcane.2da"
			   style_old="%iwdspells_data%/spell_styles_iwd.2da"
			   style_new="%iwdspells_data%/spell_styles_bg.2da"		   
END

  ACTION_IF MOD_IS_INSTALLED "spell_rev/setup-spell_rev.tp2" 55 BEGIN
    LAF run STR_VAR file=sr_nwn_spelldeflection location="%iwdspells_lib%" version=sr_nwn_arcane END
  END

  LAF run STR_VAR file=rebuild_spell_hotkeys location="%iwdspells_lib%" END



BEGIN @7 DESIGNATED 2000 // install divine resources

REQUIRE_PREDICATE GAME_IS "bgee bg2ee eet" @6

LAF make_label STR_VAR label=dw#iwd_divine END
LAF include STR_VAR file=lib_spell_style.tph location="%iwdspells_lib%" END
LAF include STR_VAR file=install_iwd_spell_resources.tpa location="%iwdspells_lib%" END
LAF install_iwd_spell_resources
           INT_VAR 
				truncate_at_level=20
		   STR_VAR
               spell_list=divine_resrefs.txt
               lookup=iwd_strref.txt
               proj_list=divine_proj.txt
               smtables_list=divine_smtable.txt
               scroll_price_list=scroll_prices_divine.txt
               tra=iwdspells.tra
               biff="dw#iwdp"
               afterwards="divine_spells_postproduction"
               afterwards_tra="dw_iwdspells_divine.tra"
               bam_copy="bam_copy_divine.2da"
               green_bams=""
			   style_old="%iwdspells_data%/spell_styles_iwd.2da"
			   style_new="%iwdspells_data%/spell_styles_bg.2da"
END


  ACTION_IF MOD_IS_INSTALLED "spell_rev/setup-spell_rev.tp2" 55 BEGIN
    LAF run STR_VAR file=sr_nwn_spelldeflection location="%iwdspells_lib%" version=sr_nwn_divine END
  END

  LAF run STR_VAR file=rebuild_spell_hotkeys location="%iwdspells_lib%" END

BEGIN @8 DESIGNATED 3000 // install bardsongs

REQUIRE_PREDICATE GAME_IS "bgee bg2ee eet" @6

LAF make_label STR_VAR label=dw#iwd_bardic END
LAF include STR_VAR file=lib_spell_style.tph location="%iwdspells_lib%" END
LAF include STR_VAR file=install_iwd_spell_resources.tpa location="%iwdspells_lib%" END
LAF install_iwd_spell_resources
           INT_VAR 
				truncate_at_level=20
		   STR_VAR
               spell_list=bardic_resrefs.txt
               lookup=iwd_strref.txt
               proj_list=bardic_proj.txt
               smtables_list=bardic_smtable.txt
               scroll_price_list=scroll_prices_arcane.txt
               tra=iwdspells.tra
               biff="dw#iwdb"
               afterwards="bardsong_postproduction"
               afterwards_tra="dw_iwdspells_bard.tra"
               bam_copy="bam_copy_divine.2da"
               green_bams=""
			   style_old="%iwdspells_data%/spell_styles_iwd.2da"
			   style_new="%iwdspells_data%/spell_styles_bg.2da"
END


////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////
////	IWDEE bit - runs to collect resources
////
////     Not really worth TRA-ifying this bit, only modders and power users
////     who can cope with English are going to be using it, so don't
////     force anyone to translate it.
////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////


BEGIN "Collect all IWD resources" DESIGNATED 90000
REQUIRE_PREDICATE GAME_IS iwdee "This component runs only when installing on BG(2)EE; it is not needed nor used when collecting IWDEE resources."
LAF include STR_VAR file=get_iwd_spell_resources.tpa location="%iwdspells_lib%" END

// arcane spells

LAF get_iwd_spell_resources
            STR_VAR
                file=iwd_arcane.2da 
                location="%iwdspells_data%"
                smtables_list=arcane_smtable.txt
                proj_list="arcane_proj.txt"
                resref_list="arcane_resrefs.txt"
END

// divine spells

LAF get_iwd_spell_resources
            INT_VAR
                CMW_string=1
            STR_VAR
                file=iwd_divine.2da
                location="%iwdspells_data%"
                smtables_list=divine_smtable.txt
                proj_list="divine_proj.txt"
                resref_list="divine_resrefs.txt"
END

// bardsong

LAF get_iwd_spell_resources
            STR_VAR
                file=iwd_bard.2da
                location="%iwdspells_data%"
                smtables_list=bardic_smtable.txt  // not needed really
                proj_list="bardic_proj.txt"
                resref_list="bardic_resrefs.txt"
END

// animations

LAF include STR_VAR file=collect_animations.tpa location="%iwdspells_lib%" END
ACTION_BASH_FOR "%scsroot%/%iwdspells_data%/anim" ".*\.ini" BEGIN
   LAF collect_iwd_animations STR_VAR ini="%BASH_FOR_FILESPEC%" anim_loc="%resource_loc%/anim/bam" wav_loc="%resource_loc%/anim/wav" END
END

// icon strings and BAMs

LAF get_iwd_icons END 

// copy-only spell BAMs

LAF include STR_VAR file=get_iwd_spell_bams.tpa location="%iwdspells_lib%" END
LAF get_iwd_spell_bams STR_VAR file="bam_copy_arcane.2da" END
LAF get_iwd_spell_bams STR_VAR file="bam_copy_divine.2da" END

// TRA entries

LAF process_tra_entries_all END




