DEFINE_ACTION_FUNCTION cd_bardic_post BEGIN


  // Kaudies protects from some BG-specific sound effects
  COPY_EXISTING ~%BARD_SONG_KAUDIES%.spl~  ~override~
    LPF CLONE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = spwi431 resource = spin789 END // shout > demilich howl
    LPF CLONE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = spwi431 resource = spin891 END // shout > moon dog howl
    LPF CLONE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = spwi431 resource = spcl908 END // shout > war cry

  ACTION_IF !enhanced_edition BEGIN
    
    // update descriptions for oBG2 bards
    ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_bard_clabs BEGIN 9562, clabba01, BARD => 1 END // add default bard
    COPY_EXISTING ~kitlist.2da~ ~override~
      COUNT_2DA_ROWS 9 rows
      FOR (index = 2 ; index < rows ; ++index) BEGIN // skip reserve row
        READ_2DA_ENTRY index 8 9 class
        PATCH_IF class = 5 BEGIN // bard
          READ_2DA_ENTRY index 1 9 kitid
          READ_2DA_ENTRY index 4 9 desc
          READ_2DA_ENTRY index 5 9 clab
          DEFINE_ASSOCIATIVE_ARRAY cd_bard_clabs BEGIN "%desc%", "%clab%", "%kitid%" => 0 END
        END  
      END
      BUT_ONLY
    
    ACTION_PHP_EACH cd_bard_clabs AS params => update BEGIN

      OUTER_SET gets_songs = 0         
      ACTION_IF ((RESOURCE_CONTAINS ~%params_1%.2da~ ~[ %TAB%]AP_FJBARD[ %TAB%]~) OR  // fixpack
                 (RESOURCE_CONTAINS ~%params_1%.2da~ ~[ %TAB%]AP_CDIBARD[ %TAB%]~) OR // iwdification
                 (update = 1)) BEGIN // forced for mainline descript
        OUTER_SET gets_songs = 1   
      END  
      
      ACTION_GET_STRREF params_0 desc
      
      ACTION_IF gets_songs BEGIN
        OUTER_SET modified = 0      
        OUTER_INNER_PATCH_SAVE desc ~%desc%~ BEGIN  
          SPRINT old @201
          REPLACE_EVALUATE "\(.*\)%old%.*" BEGIN
            SET modified = 1 
          END "%MATCH1%DW_PLACEHOLDER"  
          PATCH_FOR_EACH tra_ref IN 202 203 204 BEGIN
            SPRINT old (AT tra_ref)
            REPLACE_TEXTUALLY "[%WNL%%LNL%%MNL%].*%old%" ""
          END
          PATCH_IF modified BEGIN
            PATCH_FOR_EACH tra_ref IN 216 215 214 213 212 211 210 BEGIN
              SPRINT new (AT tra_ref)
              REPLACE_TEXTUALLY "\(.*\)DW_PLACEHOLDER.*\([%WNL%%LNL%%MNL%]\)" "\1DW_PLACEHOLDER\2\1%new%\2"		
            END
            REPLACE_TEXTUALLY "\(.*\)DW_PLACEHOLDER.*\([%WNL%%LNL%%MNL%]\)" ""	
          END
        END
        
        ACTION_IF !modified BEGIN
          OUTER_SPRINT desc ~%desc%
~ // add line break
          ACTION_FOR_EACH tra_ref IN 210 211 212 213 214 215 216 BEGIN
            OUTER_SPRINT new (AT tra_ref)
            OUTER_SPRINT desc ~%desc% 
%new%~
          END
        END

      END ELSE BEGIN // doesn't get songs
      
        OUTER_INNER_PATCH_SAVE desc ~%desc%~ BEGIN  
          SPRINT discard @221
          SPRINT disadvantages @222
          SPRINT newline @220
          REPLACE_TEXTUALLY "%disadvantages%\(.*[%WNL%%MNL%%LNL%][^a-zA-Z0-9]*\)\([a-zA-Z0-9].*\)" "%disadvantages%\1%newline%\1\2"
          REPLACE_TEXTUALLY "[%WNL%%MNL%%LNL%][^a-zA-z]*%discard%.*" ""
        END  
      
      END
      
      STRING_SET_EVALUATE params_0 ~%desc%~

    END
    
  END  
  
  // compatibility with Garrick: Tales of a Troubadour
  ACTION_IF FILE_EXISTS_IN_GAME ~gtt#sno.spl~ BEGIN     //Compatibility with garrick-tt begins here
  
    OUTER_SET code=IDS_OF_SYMBOL ("spell" "BARD_SONG_BALLAD")
    OUTER_SET code=code - 4000 
    COPY_EXISTING ~gtt#sno.spl~ ~override~    
      LPF ALTER_EFFECT INT_VAR match_opcode = 251 STR_VAR resource = EVAL ~spcl%code%~ END

    COPY_EXISTING ~bggarrik.bcs~ ~override~
                  ~garrick.bcs~  ~override~ 
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~gtt#sno~ ~#BARD1~
      END 
      IF_EXISTS      

  END 
 
END