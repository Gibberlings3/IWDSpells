// this file is used to generate arrays for other mods to use
 // at present, it's generating arrays for caster crafting
OUTER_SET ids_offset = 1000
OUTER_SPRINT prefix sppr
OUTER_SPRINT scroll cdid
OUTER_SPRINT mc_trad Divine
OUTER_SET level_bump = 7

// start caster crafting array
OUTER_SPRINT cc_array ~ACTION_DEFINE_ASSOCIATIVE_ARRAY scroll_list BEGIN%WNL%~

ACTION_PHP_EACH IWD_spell_installed AS spl => nul BEGIN

  OUTER_SET code = IDS_OF_SYMBOL ("spell" "%spl%")
  OUTER_SET code = code - ids_offset 
  ACTION_IF ((code >= 0) AND (code < 999)) BEGIN // sanity check
  
    OUTER_SPRINT spl_file ~%prefix%%code%~
    OUTER_SPRINT scroll_file   ~%scroll%%code%~
    OUTER_INNER_PATCH_SAVE spl_level "%code%" BEGIN
      REPLACE_TEXTUALLY "\([0-9]\)[0-9][0-9]" "\1"
    END  
    OUTER_SET cast_level = ((spl_level * 2) - 1)
    ACTION_IF spl_level >= level_bump BEGIN OUTER_SET cast_level += 1 END
    
    COPY_EXISTING ~%spl_file%.spl~ ~override~
      READ_LONG   0x08 name_strref
      READ_STRREF 0x08 name
      BUT_ONLY

    OUTER_SPRINT cc_array "%cc_array%%WNL%  ~%scroll_file%~, ~%spl_file%~, ~%mc_trad%~, ~%spl_level%~, ~%cast_level%~, ~0~ => ~%scroll_file%~ // %name%"
      
  END
  
END  

// close arrays
OUTER_SPRINT cc_array ~%cc_array%%WNL%%WNL%END~

<<<<<<<<./inline/cc_array.tpa
%cc_array%
>>>>>>>>
COPY ~./inline/cc_array.tpa~ ~weidu_external/iwdification/compat_for_castercraft_%mc_trad%.tpa~ EVALUATE_BUFFER
